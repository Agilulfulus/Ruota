load "System";

static Arrays :: {
	# Sorts an array using quicksort algorithm
	# A comparator lambda may be specified as the second argument
	sort(a, comp) => n_a from {
		n_a = a;
		len a < 2 then return;
		comp == null then comp = (x, y) -> { x < y };
		local lower = [];
		local upper = [];
		local pivot = Random.int(len a);
		i in [1 : len a] do {
			i != pivot then {
				local e = a[i];
				comp(e, a[pivot])
				then lower push e;
				else upper push e;
			};
		};
		n_a = idem(lower, comp) ++ [ a[pivot] ] ++ idem(upper, comp);
	};

	# Removes element from array 'a' at position 'pos'
	remove(a, pos) => n_a from {
		n_a = a;
		pos == 1 then {
			mov n_a;
		} else pos == len a then {
			pop n_a;
		} else {
			n_a = n_a[1 :> pos, (pos + 1) : len n_a];
		};
	};

	# Removes elements of equivalent value to 'value'
	removeValue(a, value) => n_a from {
		n_a = [];
		e in a do {
			e != value then {
				n_a push e;
			};
		};
	};
}