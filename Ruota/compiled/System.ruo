R"RU0(

throw(e) => {
	_OUTER_CALL_("error", e);
};

static os :: {
	system(x) => { _OUTER_CALL_("console.system", x)[0] };
	exit() => { _OUTER_CALL_("console.exit") };
	rawFile(filename) => { _OUTER_CALL_("console.raw_file", filename)[0] };
	milli() => { _OUTER_CALL_("console.milli")[0] };
};

# DEPRECATED - use new for loop syntax:
# DO:		(first, second) in (a, b) ->> { ... };
# DO NOT:	k in pairs(a, b) ->> { ... };
# Returns a paired object array built from the contents of two equally sized arrays
# pairs(&a, &b) => p from {
# 	len a != len b then return;
# 	p = (i in [0 : len a] ->> {
# 		struct {
# 			first :&= a[i]; 
# 			second :&= b[i];	
# 			string() => { first .. "=" .. second };
# 		};
# 	});
# };

# Returns every combination of arrays a and b
lace(&a, &b) => t from {
	t = [];
	i in a ->> j in b ->> {
		t push struct {
			first :&= i;
			second :&= j;
			string() => { "(" .. first .. ", " .. second .. ")" };
		};
	};
};

static Math :: {
	PI := 3.141593;
	floor(x) => { _OUTER_CALL_("console.floor", num x)[0] };
	sqrt(x) => { x ** 0.5 };
	max(&a, &b) => { a > b then a else b };
	min(&a, &b) => { a < b then a else b };
	abs(a) => { a < 0 then -a else a };
	round(a, d) => r from {
  		m := 10 ** (d != null then d else 0);
  		r = floor(num a * m + 0.5) / m;
	};
	ceil(a) => { floor(a) == a then a else floor(a) + 1 };

	sum(a) => s from {
		s = 0;
		i in a ->> {
			s = s + i;
		};
	};
	prod(a) => p from {
		p = 1;
		i in a ->> {
			p = p * i;
		};
	};
	fact(x) => { x >= 2 then x * idem(x - 1) else x };

	# TRIG
	cos(x, t) => s from {
		x >= 2 * PI do x -= 2 * PI;
		x <= -2 * PI do x += 2 * PI;
		t == null then t = 10;
		s = 0;
		i in [0 : t] ->> {
			s += (-1 ** i) * (x ** (2 * i)) / fact(2 * i);
		};
	};

	sin(x, t) => s from {
		x >= 2 * PI do x -= 2 * PI;
		x <= -2 * PI do x += 2 * PI;
		t == null then t = 10;
		s = 0;
		i in [0 : t] ->> {
			s += (-1 ** i) * (x ** (2 * i + 1)) / fact(2 * i + 1);
		};
	};

	tan(x, t) => { sin(x, t) / cos(x, t) };

	exp(x, t) => s from {
		t == null then t = 10;
		s = 0;
		i in [0 : t] ->> {
			s += (x ** i) / fact(i);
		};
	};
};

static Random :: {
	double() => { _OUTER_CALL_("console.random")[0] };
	int(min, max) => i from {
		max == null then {
			max = min;
			min = 0;
		};
		i = Math.floor(double() * (max - min) + min);
	};
	list(&a) => { a[int(0, len a)] };
};

static Regex :: {
	search(s, e) => { _OUTER_CALL_("regex.search", s, e) };
	replace(s, r, e) => { _OUTER_CALL_("regex.replace", s, r, e)[0] };
};

static Winsock :: {
	start() => { 
		r := _OUTER_CALL_("winsock.start")[0];
		r != null then throw(r);
	};
	createSocket() => socket from {
		r := _OUTER_CALL_("winsock.create_socket");
		r[0] != null then throw(r[0]);
		socket = new Socket(r[1]);
	};
	getAddresses(url) => { _OUTER_CALL_("winsock.get_addresses", url) };

	dynamic Socket :: {
		id := null;
		init(id) => {
			self.id = id;
		};
		connect(addr, port) => {
			c in addr ->> { 
				!(c == '.' && c > '0' && c < '9') then {
					addr = Winsock.getAddresses(addr)[0]; 
					break; 
				};
			};
			r := _OUTER_CALL_("winsock.connect", num id, str addr, num port)[0];
			r != null then throw(r);
		};
		send(value) => {
			r := _OUTER_CALL_("winsock.send", id, value)[0];
			r != null then throw(r);
		};
		receive(callback) => {
			detach _OUTER_CALL_("winsock.receive", id, callback);
		};
		shutdown() => {
			r := _OUTER_CALL_("winsock.shutdown", id)[0];
			r != null then throw(r);
		};
	};
};

)RU0"