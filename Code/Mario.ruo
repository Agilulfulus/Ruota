load "System";

os.system("cls");

map_x := 49;
map_y := 19;

map := alloc map_x;
buffer := alloc map_x;
i in [0 : len map] do {
	map[i] = alloc map_y;
	buffer[i] = alloc map_y;
	j in [0 : len map[i]] do {
		map[i][j] = 0;
		j > 15 then map[i][j] = 1;
		j == 12 && i < 10 then map[i][j] = 1;
		j == 12 && i > map_x - 10 then map[i][j] = 1;
		j == 9 && i > 15 && i < map_x - 15 then map[i][j] = 1;
		i == 0 then map[i][j] = 1;
		i == map_x - 1 then map[i][j] = 1;
	};
};

pos_x := 5;
pos_y := 5;
vel_y := 0;

drawBlock(x, y, s) => {
	buffer[x][y] != s then {
		os.printat(x * 2 + 1, y + 1, s);
		buffer[x][y] = s;
	};
};

keypress() => {
	map[pos_x][pos_y + 1] == 0 then vel_y = vel_y + 0.2 else vel_y = 0;
	os.keyDown('A') && map[pos_x - 1][pos_y] == 0 then pos_x = pos_x - 1;
	os.keyDown('D') && map[pos_x + 1][pos_y] == 0 then pos_x = pos_x + 1;
	os.keyDown('W') && map[pos_x][pos_y + 1] != 0 then {
		vel_y = -1;
	};
	vel_y != 0 then {
		new_y := pos_y + vel_y;
		map[pos_x][Math.floor(new_y)] != 0 do new_y = new_y - 1;
		pos_y = Math.floor(new_y);
	};
};

drawMap() => {
	i in [pos_x - 10 :> pos_x + 10] do {
		j in [pos_y - 10 :> pos_y + 10] do {
			i >= 0 && i < map_x && j >= 0 && j < map_y then {
				i == pos_x && j == pos_y then {
					drawBlock(i, j, "@");
				} else ((i - pos_x) ** 2 + (j - pos_y) ** 2) ** 0.5 < 10	 && map[i][j] != 0 then {
					drawBlock(i, j, "#");
				} else {
					drawBlock(i, j, " ");
				};
			};
		};
	};
};

!os.keyDown('Q') do {
	keypress();
	drawMap();
};