static Colors :: {
	BLACK := 0;
	BLUE := 1;
	GREEN := 2;
	CYAN := 3;
	RED := 4;
	MAGENTA := 5;
	YELLOW := 6;
	WHITE := 7;
	GREY := 8;
	GRAY := 8;
	BRIGHT_BLUE := 9;
	BRIGHT_GREEN := 10;
	BRIGHT_CYAN := 11;
	BRIGHT_RED := 12;
	BRIGHT_MAGENTA := 13;
	BRIGHT_YELLOW := 14;
	BRIGHT_WHITE := 15;
};

static os :: {
	print(x) => { _OUTER_CALL_(1, x) };
	printl(x) => { print(x .. "\n") };
	newl() => { printl("") };
	system(x) => { _OUTER_CALL_(0, x)[1] };
	exit() => { _OUTER_CALL_(2) };
	rawFile(filename) => { _OUTER_CALL_(8, filename)[1] };
	
	color(k, k2) => { _OUTER_CALL_(7, k + 16 * k2) };

	colorMap() => {
		i in [0 : 15] do {
			j in [0 : 15] do {
				k := i + 16 * j;
				color(k);
				print(" ");
				k < 10 then {
					print("  ");
				} else k < 100 then {
					print(" ");
				};
				print(k);
			};
			newl();
		};
	};

	static Input :: {
		number() => { num string() };
		string() => { _OUTER_CALL_(3)[1] };
		line() => { _OUTER_CALL_(4)[1] };
	};
};

static Random :: {
	double() => { _OUTER_CALL_(5)[1] };
	int(min, max) => {
		max == null then 
			Math.floor(double() * (min - 1) + 1)
		else
			Math.floor(double() * (max - min) + min);
	};
};

static Math :: {
	floor(x) => { _OUTER_CALL_(6, x)[1] };
	sqrt(x) => { x ** 0.5 };
	max(a, b) => { a > b then a else b };
	min(a, b) => { a < b then a else b };
	sum(a) => s from {
		s = 0;
		i in a do {
			s = s + i;
		};
	};
	prod(a) => p from {
		p = 1;
		i in a do {
			p = p * i;
		};
	};
	fact(x) => prod([1:x]);
};

# Converts an array to a string representation
sarr(a) => s from {
	type a == "array" then {
		s = "[ ";
		i in a do {
			s = s .. idem(i) .. " ";
		};
		s = s .. "]";
	} else {
		s = a;
	};
};

# Returns a paired object array built from the contents of two equally sized arrays
pairs(a, b) => p from {
	len a != len b then return;
	p = [];
	i in [1 : len a] do {
		p push struct {
			first := a[i], 
			second := b[i]
		};
	};
};